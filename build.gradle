plugins {
	id 'java-library'
	id 'fabric-loom' version '1.3.9'
	id 'idea'
	id 'maven-publish'
	id 'com.modrinth.minotaur' version '2.+'
	id "io.freefair.lombok" version "8.2.2"
	id "com.github.johnrengelman.shadow" version '8.1.1'
  	id "org.sonarqube" version '4.3.0.3225'
}

version = project.mod_version
group = project.maven_group

sonar {
  properties {
    property "sonar.projectKey", "theomenden_TheOmenDen.Vanadium"
    property "sonar.organization", "theomenden"
    property "sonar.host", "https://sonarcloud.io"
  }
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {	name "Modrinth"; url "https://api.modrinth.com/maven"
		content { includeGroup "maven.modrinth"	}
	}
	maven {	name 'modmenu'; url 'https://maven.terraformersmc.com/releases'	}
	maven {	name 'cloth';   url = 'https://maven.shedaniel.me'	}
	maven { name 'jitpack'; url 'https://jitpack.io'}
}

loom {
	accessWidenerPath = file("src/main/resources/vanadium.accesswidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modApi( "maven.modrinth:sodium:${project.sodium_version}") {
		exclude(group: 'net.fabricmc.fabric.fabric-api')
	}
	modCompileOnly modRuntimeOnly('maven.modrinth:indium:1.0.25+mc1.20.1')
	modApi ("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
		exclude(group: 'net.fabricmc.fabric.fabric-api')
	}
	modApi ("com.terraformersmc:modmenu:${project.modmenu_version}") {
		exclude(group: 'net.fabricmc.fabric.fabric-api')
	}
	implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2';
	runtimeOnly "org.joml:joml:1.10.2"

	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.
	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version
	inputs.property "minecraft_version", project.minecraft_version
	inputs.property "loader_version", project.loader_version
	filesMatching("fabric.mod.json") {
		expand "version": project.version,
		"minecraft_version": project.minecraft_version,
		"loader_version": project.loader_version
	}
}

def targetJavaVersion = 17;

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	if(targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
		it.options.release = targetJavaVersion
	}
}

java {
	def javaVersion = JavaVersion.toVersion(targetJavaVersion)
	if(JavaVersion.current() < javaVersion) {
		toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
	}
	archivesBaseName = project.archives_base_name
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
